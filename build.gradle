// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
configurations.all {
    resolutionStrategy.eachDependency {
        DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '27.1.1'
                }
            }
    }
}
allprojects {
    repositories {
        maven { url 'http://developer.huawei.com/repo/' }
        maven { url 'https://jitpack.io' }
        google()
        jcenter()
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}
ext{
    if(!project.hasProperty('isAarRelease')){
        isAarRelease = false;
    }
    if(!project.hasProperty('isLite')){
        isLite = false;
    }
    if(!project.hasProperty('sdkVersion')){
        sdkVersion = '3.2.3'
    }
    if(!project.hasProperty('isTravis')) {
        isTravis = false
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
